/**
 * ChaiLove Benchmark
 */

// The Sprites and the image to load.
global sprites = List()
global img
global music

/**
 * ChaiLove callback; Configure the application.
 */
def conf(t) {
	t.window.width = 720
	t.window.height = 680
	t.console = true
}

/**
 * ChaiLove callback; Load the application.
 */
def load() {
	sprites
	love.graphics.setBackgroundColor(54, 172, 248)

	// Add the first sprite.
	img = love.graphics.newImage("sprite.png")
	sprites.push_back(Sprite())

	// Load the music.
	music = love.audio.newSource("music.ogg")
	music.setLooping(true)
	music.play()
}

/**
 * ChaiLove Callback; Restart the content.
 */
def reset() {
	sprites.clear()
	sprites.push_back(Sprite())
}

/**
 * ChaiLove callback; Update the game state.
 */
def update(delta) {
	// Update all the sprites.
	for (sprite : sprites) {
		sprite.update(delta)
	}

	// Allow increasing the amount of sprites.
	if (love.joystick[0].isDown("up")) {
		sprites.push_back(Sprite())
	}
	if (love.joystick[0].isDown("down") && sprites.size() > 1) {
		sprites.pop_back()
	}
}

/**
 * ChaiLove callback; Draw the game state.
 */
def draw() {
	// Draw all the sprites.
	for (sprite : sprites) {
		sprite.draw()
	}

	// Write the text on the screen.
	love.graphics.setColor(255, 255, 255)
	love.graphics.print("Sprites: " + to_string(sprites.size()), 5, 5)
	love.graphics.print("Press up/down", 5, 20)
	love.graphics.print("FPS: " + to_string(love.timer.getFPS()), 5, 35)
}

/**
 * ChaiLove callback; Mouse button pressed.
 */
def mousepressed(x, y, button) {
	sprites.push_back(Sprite())
}

/**
 * A sprite to display on the screen.
 */
class Sprite {
	var x
	var y
	var xspeed
	var yspeed

	def Sprite() {
		this.x = love.math.random(love.graphics.getWidth()) * 1.0f
		this.y = love.math.random(love.graphics.getHeight()) * 1.0f
		this.xspeed = love.math.random(10) * 50.0f
		this.yspeed = love.math.random(10) * 50.0f
	}

	def draw() {
		love.graphics.draw(img, this.x, this.y)
	}

	def update(delta) {
		this.x += this.xspeed * delta
		this.y += this.yspeed * delta

		if (this.x + img.getWidth() > love.graphics.getWidth()) {
			this.xspeed = this.xspeed * -1.0f
			this.x = love.graphics.getWidth() - img.getWidth()
		}
		if (this.x < 0) {
			this.xspeed = this.xspeed * -1.0f
			this.x = 0
		}
		if (this.y + img.getHeight() > love.graphics.getHeight()) {
			this.yspeed = this.yspeed * -1.0f
			this.y = love.graphics.getHeight() - img.getHeight()
		}
		if (this.y < 0) {
			this.yspeed = this.yspeed * -1.0f
			this.y = 0
		}
	}
};

def loadstate(data) {
	var info = from_json(data)
	var num = info["number"]

	// Do something with the loaded number.
	sprites.clear()
	for (var i = 0; i < num; ++i) {
		sprites.push_back(Sprite())
	}

	// The state loaded correctly, so return true.
	return true
}

def savestate() {
	// Retrieve the number to save.
	var num = sprites.size()

	// Construct the JSON data.
	var data = ["number": num]

	// Tell the system to save the JSON data.
	return to_json(data)
}
