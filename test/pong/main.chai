global players = []
global pongSound
global ball

def conf(t) {
  t.window.width = 640
  t.window.height = 480
}

def load() {
  filesystem.load("Player.chai")
  players = [
    Player(false, 80.0f, graphics.getHeight() / 2.0f),
    Player(true, graphics.getWidth() - 80.0f, graphics.getHeight() / 2.0f)
  ]

  graphics.setBackgroundColor(200, 200, 240)
  pongSound = sound.newSoundData("pong.wav", "chunk")
  ballReset()
}

def update(delta) {
  for (var i = 0; i < 2; ++i) {
    players[i].update(delta)
  }

  if (ball["yspeed"] < 0) {
    if (ball["y"] < 0) {
        ball["yspeed"] *= -1
    }
  }
  else if (ball["yspeed"] > 0) {
    if (ball["y"] > graphics.getHeight()) {
        ball["yspeed"] *= -1
    }
  }
  if (ball["x"] < 0) {
    ballReset()
  }
  else if (ball["x"] > graphics.getWidth()) {
    ballReset()
  }

  if (ball["xspeed"] <= 0) {
    if (ballCollide(players[0])) {
      ball["xspeed"] *= -1
    }
  }
  else {
    if (ballCollide(players[1])) {
      ball["xspeed"] *= -1
    }
  }

  ball["x"] += ball["xspeed"] * delta
  ball["y"] += ball["yspeed"] * delta
}

def draw() {
  for (var i = 0; i < 2; ++i) {
    players[i].draw()
  }
  graphics.setColor(183, 28, 28)
  graphics.circle("fill", ball["x"], ball["y"], ball["radius"]);
}

def ballReset() {
  ball = [
    "x": graphics.getWidth() / 2.0f,
    "y": graphics.getHeight() / 2.0f,
    "xspeed": 0.2f,
    "yspeed": 0.05f,
    "radius": 5
  ]
}

def ballCollide(player) {
  if (ball["y"] + ball["radius"] <= player.y) {
    return false
  }
  if (ball["y"] - ball["radius"]>= player.y + player.height) {
    return false
  }
  if (ball["x"] + ball["radius"] <= player.x) {
    return false
  }
  if (ball["x"] - ball["radius"] > player.x + player.width) {
    return false
  }
  if (ball["yspeed"] > 0) {
    if (ball["y"] < player.y + player.height / 4) {
      ball["yspeed"] *= -1
    }
  }
  else if (ball["yspeed"] < 0) {
    if (ball["y"] > player.y + player.height / 2 + player.height / 4) {
      ball["yspeed"] *= -1
    }
  }
  audio.play(pongSound)
  ball["xspeed"] *= 1.1f
  return true
}
